import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.17'
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

def getProtobufArtifact() {
    def module = "com.google.protobuf:protoc"
    def version = "3.8.0"
    if (Os.isFamily(Os.FAMILY_MAC)) {
        return "$module:$version:osx-x86_64"
    }

    return "$module:$version"
}

publishing {
    publications {
        register("release", MavenPublication) {
            groupId = "io.owow"
            artifactId = "esp-idf-provisioning-android"
            version = "2.0.12.2"

            afterEvaluate {
                from(components.release)
            }
        }
    }
}

def protobufArtifact = getProtobufArtifact()

protobuf {
    protoc {
        artifact = "$protobufArtifact"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'com.google.protobuf:protobuf-javalite:3.14.0'
    implementation 'com.google.crypto.tink:tink-android:1.6.1'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.github.yuriy-budiyev:code-scanner:2.1.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
